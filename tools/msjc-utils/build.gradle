plugins {
    id "com.jfrog.artifactory" version "4.17.1"
    id "com.jfrog.bintray" version "1.8.5"
    id "maven-publish"
}

dependencies {
    api "io.vavr:vavr:${vavrVersion}"
    api "io.vavr:vavr-jackson:${vavrVersion}"

    api "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    api "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    api "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    api "com.fasterxml.jackson.module:jackson-module-parameter-names:${jacksonVersion}"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    api "com.googlecode.libphonenumber:libphonenumber:${libphonenumberVersion}"

    api "org.modelmapper:modelmapper:${modelmapperVersion}"
    api "com.github.spullara.mustache.java:compiler:${mustacheJavaCompilerVersion}"

    api "com.google.guava:guava:${guavaVersion}"
    api "org.slf4j:slf4j-api:${slf4jVersion}"
}

test {
    useJUnitPlatform()
}

// run gradle with "-Dsnapshot=true" to automatically append "-SNAPSHOT" to the version
version = '0.0.1' + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")
sourceCompatibility = 1.8

ext{
    bintrayUser = System.getProperty("bintray.user")
    bintrayKey = System.getProperty("bintray.key")
    buildNumber = System.getProperty("build.number")
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "wx-chevalier"
            name "Chevalier"
            email "384924552@qq.com"
        }
    }

    scm {
        url "https://github.com/wx-chevalier/msjc-utils"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'cn.msjc.utils'
            artifactId 'msjc-utils'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Java Commons & Boilerplates')
                root.appendNode('name', 'ms-java-common')
                root.appendNode('url', 'https://github.com/wx-chevalier/msjc-utils')
                root.children().last() + pomConfig
            }
        }
    }
}

artifactory {
    contextUrl = 'http://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = bintrayUser
            password = bintrayKey
        }
        defaults {
            publications('mavenPublication')
            publishArtifacts = true
            publishPom = true
            properties = [
                    'build.number': buildNumber,
                    'build.name': 'msjc-utils'
            ]
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
    clientConfig.info.setBuildNumber(buildNumber)
    clientConfig.info.setBuildName('msjc-utils')
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenPublication']

    pkg {
        userOrg = 'ms-java-commons'
        repo = 'msjc-utils'
        name = 'msjc-utils'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/wx-chevalier/msjc-utils'
        version {
            name = project.version
            desc = "build ${buildNumber}"
            released  = new Date()
            gpg {
                sign = true
            }
        }
    }

    publish = true
}